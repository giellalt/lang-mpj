!! Noun inflection
!  ---------------
!! The Wangkajunga language nouns inflect in cases.

LEXICON NounReduplication	! not sure how to do this yet (there is a chapter in B&K on redupl)
		n_agr ;

LEXICON n_agr  
+N: x_agr ; ! This is the "General noun stem" of the floatchart

! Ergativity: Abs IV, Erg [ TV Abs ]

LEXICON x_agr ! This is the "General noun stem" of the floatchart
derivational_case_1 ;
semantic_case_1 ;
grammatical_case_1 ;
derivational_avoid_case ;
! focus ;
! inchoative;	! NB from the book, inchoative is used as verbalisation
! verb_derivation_ti;

LEXICON derivational_case_1
! leading to derivational_case_2
				derivational_case_2 ;
@U.DCASE.HAV@+Der/Hav:@U.DCASE.HAV@%>kurlu		derivational_case_2 ;		! having
@U.DCASE.THING@+Der/Thing:@U.DCASE.THING@%>pinti	derivational_case_2 ;		! associated thing
@U.DCASE.PRIV@+Der/Priv:@U.DCASE.PRIV@%>parni		derivational_case_2 ;		! privative
@U.DCASE.INT@+Der/Int:@U.DCASE.INT@%>kija		derivational_case_2 ;		! intentive
@U.DCASE.WANT@+Der/Want:@U.DCASE.WANT@%>jiraja		derivational_case_2 ;		! want
@U.DCASE.ASST@+Der/Asst:@U.DCASE.ASST@%>jarra	derivational_case_2 ;		! associated time or space
@U.DCASE.TEMP@+Der/Temp:@U.DCASE.TEMP@%>puru		derivational_case_2 ;		! temporal
@U.DCASE.DWELL@+Der/Dwell:@U.DCASE.DWELL@%>karraja		derivational_case_2 ;		! dweller
@U.DCASE.SIDE@+Der/Side:@U.DCASE.SIDE@%>karti		derivational_case_2 ;		! side
@U.DCASE.TYPE@+Der/Type:@U.DCASE.TYPE@%>partu		derivational_case_2 ;		! type
@U.DCASE.SIM@+Der/Sim:@U.DCASE.SIM@%>yuru			derivational_case_2 ;		! similar
@U.DCASE.CONTR@+Der/Contr:@U.DCASE.CONTR@%>munu		derivational_case_2 ;		! contradictive
@U.DCASE.MOD@+Der/Mod:@U.DCASE.MOD@%>marta		derivational_case_2 ;		! moderative
@U.DCASE.BIG@+Der/Big:@U.DCASE.BIG@%>paka			derivational_case_2 ;		! very big
@U.DCASE.ANOTH@+Der/Anoth:@U.DCASE.ANOTH@%>kujupa		derivational_case_2 ;		! another
@U.DCASE.VERY@+Der/Very:@U.DCASE.VERY@%>minyirri		derivational_case_2 ;		! very
@U.DCASE.NUM@+Der/Num:@U.DCASE.NUM@%>marlu		derivational_case_2 ;		! numerative
@U.DCASE.DUAL@+Der/Dual:@U.DCASE.DUAL@%>kujarra		derivational_case_2 ;		! dual
@U.DCASE.FEW@+Der/Few:@U.DCASE.FEW@%>paraku		derivational_case_2 ;		! few
@U.DCASE.PL@+Der/Pl:@U.DCASE.PL@%>n		derivational_case_2 ;		! plural alternative 1
@U.DCASE.PL@+Der/Pl:@U.DCASE.PL@%>rti		derivational_case_2 ;		! plural alternative 2
@U.DCASE.GRP@+Der/Grp:@U.DCASE.GRP@%>ngurun	derivational_case_2 ;	! group *afterwards comes PA
@U.DCASE.PAIR@+Der/Pair:@U.DCASE.PAIR@%>rarra		derivational_case_2 ;		! pair
@U.DCASE.ONLY@+Der/Only:@U.DCASE.ONLY@%>wiyaju		derivational_case_2 ;		! only
@U.DCASE.FOC@+Der/Foc:@U.DCASE.FOC@%>nguninji derivational_case_2 ;! focus (sometimes annotated as SPEC)
! leading to semantic_case_2
				semantic_case_2 ;	
+Der/Hav:%>kurlu		semantic_case_2 ;
+Der/Thing:%>pinti		semantic_case_2 ;
+Der/Priv:%>parni		semantic_case_2 ;
+Der/Int:%>kija			semantic_case_2 ;
+Der/Want:%>jiraja		semantic_case_2 ;
+Der/Asst:%>jarra		semantic_case_2 ;
+Der/Temp:%>puru		semantic_case_2 ;
+Der/Dwell:%>karraja		semantic_case_2 ;
+Der/Side:%>karti		semantic_case_2 ;
+Der/Type:%>partu		semantic_case_2 ;
+Der/Sim:%>yuru			semantic_case_2 ;
+Der/Contr:%>munu		semantic_case_2 ;
+Der/Mod:%>marta		semantic_case_2 ;
+Der/Big:%>paka			semantic_case_2 ;
+Der/Anoth:%>kujupa		semantic_case_2 ;
+Der/Very:%>minyirri		semantic_case_2 ;
+Der/Num:%>marlu		semantic_case_2 ;
+Der/Dual:%>kujarra		semantic_case_2 ;
+Der/Few:%>paraku		semantic_case_2 ;
+Der/Pl:%>n			semantic_case_2 ;
+Der/Pl:%>rti			semantic_case_2 ;
+Der/Grp:%>ngurun		semantic_case_2 ;
+Der/Pair:%>rarra		semantic_case_2 ;
+Der/Only:%>wiyaju		semantic_case_2 ;
+Der/Foc:%>nguninji		semantic_case_2 ;

LEXICON semantic_case_1
			derivational_case_3 ;
+Abl:%>janu		derivational_case_3 ;	! Ablative (motion away from a place)
+Abl:%>nguru		derivational_case_3 ;
+Gen:%>kurnu		derivational_case_3 ;	! Genitive (referred to as POSS - possessive - in book)
! +Gen:%>kura		derivational_case_3 ;	! kurnu is by far the most common GEN form
! +Gen:%>kurangu	derivational_case_3 ;
! +Loc:%>ngka		derivational_case_3 ;	! Locative - following a vowel (better done in twolc)
! +Loc:%>ta		derivational_case_3 ;	! following a consonant (difference betw t and j?)
! +Loc:%>ja		derivational_case_3 ;	! following a consonant
+Loc:%>		derivational_case_3 ;
+Perl:%>wana		derivational_case_3 ;	! Perlative (motion along / across a place)
+All:%>kutu		derivational_case_3 ;	! Allative (motion towards a place)

LEXICON grammatical_case_1
	derivational_focus_case ;
+Abs: 	derivational_focus_case ;
+Erg:%>lu derivational_focus_case; ! will do different ergative forms in twolc
+Erg:%>tu derivational_focus_case;
+Erg:%>ju derivational_focus_case;
+Dat:%>ku derivational_focus_case;

LEXICON derivational_avoid_case
+Avoid:%>ngkamarra	# ;	! Avoid (following a vowel) (will do in twolc)
+Avoid:%>jamarra	# ;	! Avoid (following a consonant)

LEXICON derivational_case_2
				grammatical_case_3 ;
@D.DCASE.HAV@+Der/Hav:@D.DCASE.HAV@%>kurlu		grammatical_case_3 ;
@D.DCASE.THING@+Der/Thing:@D.DCASE.THING@%>pinti	grammatical_case_3 ;
@D.DCASE.PRIV@+Der/Priv:@D.DCASE.PRIV@%>parni		grammatical_case_3 ;
@D.DCASE.INT@+Der/Int:@D.DCASE.INT@%>kija		grammatical_case_3 ;
@D.DCASE.WANT@+Der/Want:@D.DCASE.WANT@%>jiraja		grammatical_case_3 ;
@D.DCASE.ASST@+Der/Asst:@D.DCASE.ASST@%>jarra		grammatical_case_3 ;
@D.DCASE.TEMP@+Der/Temp:@D.DCASE.TEMP@%>puru		grammatical_case_3 ;
@D.DCASE.DWELL@+Der/Dwell:@D.DCASE.DWELL@%>karraja	grammatical_case_3 ;
@D.DCASE.SIDE@+Der/Side:@D.DCASE.SIDE@%>karti		grammatical_case_3 ;
@D.DCASE.TYPE@+Der/Type:@D.DCASE.TYPE@%>partu		grammatical_case_3 ;
@D.DCASE.SIM@+Der/Sim:@D.DCASE.SIM@%>yuru		grammatical_case_3 ;
@D.DCASE.CONTR@+Der/Contr:@D.DCASE.CONTR@%>munu		grammatical_case_3 ;
@D.DCASE.MOD@+Der/Mod:@D.DCASE.MOD@%>marta		grammatical_case_3 ;
@D.DCASE.BIG@+Der/Big:@D.DCASE.BIG@%>paka		grammatical_case_3 ;
@D.DCASE.ANOTH@+Der/Anoth:@D.DCASE.ANOTH@%>kujupa	grammatical_case_3 ;
@D.DCASE.VERY@+Der/Very:@D.DCASE.VERY@%>minyirri	grammatical_case_3 ;
@D.DCASE.NUM@+Der/Num:@D.DCASE.NUM@%>marlu		grammatical_case_3 ;
@D.DCASE.DUAL@+Der/Dual:@D.DCASE.DUAL@%>kujarra		grammatical_case_3 ;
@D.DCASE.FEW@+Der/Few:@D.DCASE.FEW@%>paraku		grammatical_case_3 ;
@D.DCASE.PL@+Der/Pl:@D.DCASE.PL@%>n			grammatical_case_3 ;
@D.DCASE.PL@+Der/Pl:@D.DCASE.PL@%>rti			grammatical_case_3 ;
@D.DCASE.GRP@+Der/Grp:@D.DCASE.GRP@%>ngurun		grammatical_case_3 ;
@D.DCASE.PAIR@+Der/Pair:@D.DCASE.PAIR@%>rarra		grammatical_case_3 ;
@D.DCASE.ONLY@+Der/Only:@D.DCASE.ONLY@%>wiyaju		grammatical_case_3 ;
@D.DCASE.FOC@+Der/Foc:@D.DCASE.FOC@%>nguninji		grammatical_case_3 ;

LEXICON semantic_case_2
! leading to grammatical_case_3
			grammatical_case_3 ;
+Abl:%>janu		grammatical_case_3 ;
+Abl:%>nguru		grammatical_case_3 ;
+Gen:%>kurnu		grammatical_case_3 ;
! +Gen:%>kura		grammatical_case_3 ;
! +Gen:%>kurangu	grammatical_case_3 ;
! +Loc:%>ngka		grammatical_case_3 ;
! +Loc:%>ta		grammatical_case_3 ;
! +Loc:%>ja		grammatical_case_3 ;
+Loc:%>		grammatical_case_3 ;
+Perl:%>wana		grammatical_case_3 ;
+All:%>kutu		grammatical_case_3 ;
! leading to semantic_case_3
			semantic_case_3 ;
@U.SCASE.ABL@+Abl:@U.SCASE.ABL@%>janu		semantic_case_3 ;
@U.SCASE.ABL@+Abl:@U.SCASE.ABL@%>nguru		semantic_case_3 ;
@U.SCASE.GEN@+Gen:@U.SCASE.GEN@%>kurnu		semantic_case_3 ; ! kurnu is by far the most common form
! @U.SCASE.GEN@+Gen:@U.SCASE.GEN@%>kura		semantic_case_3 ;
! @U.SCASE.GEN@+Gen:@U.SCASE.GEN@%>kurangu	semantic_case_3 ;
! @U.SCASE.LOC@+Loc:@U.SCASE.LOC@%>ngka		semantic_case_3 ;
! @U.SCASE.LOC@+Loc:@U.SCASE.LOC@%>ta		semantic_case_3 ;
! @U.SCASE.LOC@+Loc:@U.SCASE.LOC@%>ja		semantic_case_3 ;
@U.SCASE.LOC@+Loc:@U.SCASE.LOC@%>		semantic_case_3 ;
@U.SCASE.PERL@+Perl:@U.SCASE.PERL@%>wana	semantic_case_3 ;
@U.SCASE.ALL@+All:@U.SCASE.ALL@%>kutu		semantic_case_3 ;

LEXICON derivational_focus_case
end_noun_cases ;
+Der/Foc:%>nguninji		end_noun_cases;		! focus (sometimes annotated as SPEC)

LEXICON derivational_case_3
				end_noun_cases ;
+Der/Hav:%>kurlu		end_noun_cases ;
+Der/Thing:%>pinti		end_noun_cases ;
+Der/Priv:%>parni		end_noun_cases ;
+Der/Int:%>kija			end_noun_cases ;
+Der/Want:%>jiraja		end_noun_cases ;
+Der/Asst:%>jarra		end_noun_cases ;
+Der/Temp:%>puru		end_noun_cases ;
+Der/Dwell:%>karraja		end_noun_cases ;
+Der/Side:%>karti		end_noun_cases ;
+Der/Type:%>partu		end_noun_cases ;
+Der/Sim:%>yuru			end_noun_cases ;
+Der/Contr:%>munu		end_noun_cases ;
+Der/Mod:%>marta		end_noun_cases ;
+Der/Big:%>paka			end_noun_cases ;
+Der/Anoth:%>kujupa		end_noun_cases ;
+Der/Very:%>minyirri		end_noun_cases ;
+Der/Num:%>marlu		end_noun_cases ;
+Der/Dual:%>kujarra		end_noun_cases ;
+Der/Few:%>paraku		end_noun_cases ;
+Der/Pl:%>n			end_noun_cases ;
+Der/Pl:%>rti			end_noun_cases ;
+Der/Grp:%>ngurun		end_noun_cases ;
+Der/Pair:%>rarra		end_noun_cases ;
+Der/Only:%>wiyaju		end_noun_cases ;
+Der/Foc:%>nguninji		end_noun_cases ;

LEXICON semantic_case_3
			end_noun_cases ;
@D.SCASE.ABL@+Abl:@D.SCASE.ABL@%>janu		end_noun_cases ;
@D.SCASE.ABL@+Abl:@D.SCASE.ABL@%>nguru		end_noun_cases ;
@D.SCASE.GEN@+Gen:@D.SCASE.GEN@%>kurnu		end_noun_cases ;
! @D.SCASE.GEN@+Gen:@D.SCASE.GEN@%>kura		end_noun_cases ;
! @D.SCASE.GEN@+Gen:@D.SCASE.GEN@%>kurangu	end_noun_cases ;
! @D.SCASE.LOC@+Loc:@D.SCASE.LOC@%>ngka		end_noun_cases ;
! @D.SCASE.LOC@+Loc:@D.SCASE.LOC@%>ta		end_noun_cases ;
! @D.SCASE.LOC@+Loc:@D.SCASE.LOC@%>ja		end_noun_cases ;
@D.SCASE.LOC@+Loc:@D.SCASE.LOC@%>		end_noun_cases ;
@D.SCASE.PERL@+Perl:@D.SCASE.PERL@%>wana	end_noun_cases ;
@D.SCASE.ALL@+All:@D.SCASE.ALL@%>kutu		end_noun_cases ;

LEXICON grammatical_case_3
	end_noun_cases ;
+Abs: 	end_noun_cases ;
+Erg:%>lu end_noun_cases ; ! will do different ergative forms in twolc
+Erg:%>tu end_noun_cases ;
+Erg:%>ju end_noun_cases ;
+Dat:%>ku end_noun_cases ;

LEXICON end_noun_cases
 PA ;
:%> K_1 ; ! no person-number suffixed 
! person_number_case_1 ; ! XXX is _1 the intendend lexicon?


!! !! temporal and spatial nouns - have a limited set of specific case endings, and do not have pronoun
!! !! clitics
LEXICON n_temporal_agr
+N+Sem/Temp:%> temporal_case ;

LEXICON n_spatial_agr
! +N+Sem/Spat:%> # ;
+N+Sem/Spat: spatial_case ;

LEXICON temporal_case
		# ;
+SpatLoc:%>ngka	# ;
+Der/Asst:%>jarra	# ;

LEXICON spatial_case
			# ;
+Der/Anoth:%>kujupa	# ;
+Der/Side:%>karti	# ;
+Der/SpatAbl:%>mpanguru	# ;
+Der/SpatAll:%>warraku	# ;

